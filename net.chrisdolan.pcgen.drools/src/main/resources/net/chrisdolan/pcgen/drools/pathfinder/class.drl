package net.chrisdolan.pcgen.drools.pathfinder;
    dialect "java"

import net.chrisdolan.pcgen.drools.input.*;
import net.chrisdolan.pcgen.drools.type.*;

query "Query.Class.Level" (String classname)
    Input(type == "ClassLevel", classname == subtype, level : value)
end
query "Query.Class.Level.All"
    Input(type == "ClassLevel", classname : subtype, level : value)
end

rule "Class.Init"
    salience 1000
    when
    then
        insertLogical(new StackRule("BAB", "ClassLevel", "Sum"));
        insertLogical(new ClassName("Barbarian"));
        insertLogical(new ClassName("Bard"));
        insertLogical(new ClassName("Cleric"));
        insertLogical(new ClassName("Druid"));
        insertLogical(new ClassName("Fighter"));
        insertLogical(new ClassName("Monk"));
        insertLogical(new ClassName("Paladin"));
        insertLogical(new ClassName("Ranger"));
        insertLogical(new ClassName("Rogue"));
        insertLogical(new ClassName("Sorcerer"));
        insertLogical(new ClassName("Wizard"));
end

declare ClassName
    name : String
end
declare ArchetypeName
    classname : String
    name : String
end
declare Archetype
    classname : String
    name : String
end
declare SpellCasterClass
    classname : String
end

rule "Class.Level.Total"
    when
        MaxClassLevel($class:classname)
        Number(intValue > 0, $sum : intValue) from accumulate(
            $l : LevelInput(classname == $class),
            count($l)
        )
    then
        insertLogical(new Input("ClassLevel", $class, $sum));
end
rule "Class.Archetype.UserInput"
    when
        Input(type == "ClassLevel", $class:subtype)
        ArchetypeInput($class == classname, name != "Base", $name : name)
    then
        insertLogical(new Archetype($class, $name));
end
rule "Class.Archetype.Base"
    when
        Input(type == "ClassLevel", $class:subtype)
        //not ArchetypeInput($class == classname, name != "Base")
    then
        insertLogical(new Archetype($class, "Base"));
end
/*
rule "Class.Archetype"
    when
        ArchetypeInput($class:classname,$name:name)
    then
        insertLogical(new Input("Archtype", $class+"."+$name, 1);
end
*/

declare MaxClassLevel
    classname : String
    value : int
end
declare HitDie
    classname : String
    value : int
end
declare BABProgression
    classname : String
    type : String
end
declare SaveProgression
    classname : String
    save : String
    type : String
end

rule "Class.BAB.Progression.Fast"
    when
        Input(type == "ClassLevel", $class:subtype, $v:value)
        BABProgression(classname == $class, type == "Fast")
    then
        insertLogical(new Input("BAB", "ClassLevel"+$class, $v));
end
rule "Class.BAB.Progression.Medium"
    when
        Input(type == "ClassLevel", $class:subtype, $v:value)
        BABProgression(classname == $class, type == "Medium")
    then
        insertLogical(new Input("BAB", "ClassLevel"+$class, $v*3/4));
end
rule "Class.BAB.Progression.Slow"
    when
        Input(type == "ClassLevel", $class:subtype, $v:value)
        BABProgression(classname == $class, type == "Slow")
    then
        insertLogical(new Input("BAB", "ClassLevel"+$class, $v/2));
end

rule "Class.Save.Progression.Fast"
    when
        Input(type == "ClassLevel", $class:subtype, $v:value)
        SaveProgression(classname == $class, $save:save, type == "Fast")
    then
        insertLogical(new Input(SavingThrow.TYPE + $save, "ClassLevel"+$class, 2+$v/2));
end
rule "Class.Save.Progression.Slow"
    when
        Input(type == "ClassLevel", $class:subtype, $v:value)
        SaveProgression(classname == $class, $save:save, type == "Slow")
    then
        insertLogical(new Input(SavingThrow.TYPE + $save, "ClassLevel"+$class, $v/3));
end


rule "Validation.Class.Unknown"
    when
        $i : Input(type == "ClassLevel", $classname:subtype)
        not ClassName(name == $classname)
    then
        insertLogical(new InvalidInputViolation("Class.Unknown", "Unknown class " + $classname, new Object[]{$i}));
end

rule "Validation.Archetype.Unknown"
    when
        $a : Archetype($classname:classname, $name:name, name != "Base")
        not ArchetypeName(classname == $classname, name == $name)
    then
        insertLogical(new InvalidInputViolation("Archetype.Unknown", "Unknown archetype " + $classname + ", " + $name, new Object[]{$a}));
end
