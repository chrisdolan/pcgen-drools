package net.chrisdolan.pcgen.drools;
    dialect "java"

import net.chrisdolan.pcgen.drools.input.*;
import net.chrisdolan.pcgen.drools.type.*;

rule "Class.Init"
    when
    then
        insertLogical(new StackRule("BAB", "ClassLevel", "Sum"));
end

declare BABProgression
    classname : String
    type : String
end
declare SaveProgression
    classname : String
    save : String
    type : String
end

rule "Class.BAB.Progression.Fast"
    when
        Input(type == "ClassLevel", $st:subtype, value > 0, $v:value)
        BABProgression(classname == $st, type == "Fast")
    then
        insertLogical(new Input("BAB", "ClassLevel", $v));
end
rule "Class.BAB.Progression.Medium"
    when
        Input(type == "ClassLevel", $st:subtype, value > 0, $v:value)
        BABProgression(classname == $st, type == "Medium")
    then
        insertLogical(new Input("BAB", "ClassLevel", $v*3/4));
end
rule "Class.BAB.Progression.Slow"
    when
        Input(type == "ClassLevel", $st:subtype, value > 0, $v:value)
        BABProgression(classname == $st, type == "Slow")
    then
        insertLogical(new Input("BAB", "ClassLevel", $v/2));
end

rule "Class.Save.Progression.Fast"
    when
        Input(type == "ClassLevel", $st:subtype, value > 0, $v:value)
        SaveProgression(classname == $st, $save:save, type == "Fast")
    then
        insertLogical(new Input(SavingThrow.TYPE + $save, "ClassLevel", ($v+4)/2));
end
rule "Class.Save.Progression.Slow"
    when
        Input(type == "ClassLevel", $st:subtype, value > 0, $v:value)
        SaveProgression(classname == $st, $save:save, type == "Slow")
    then
        insertLogical(new Input(SavingThrow.TYPE + $save, "ClassLevel", ($v+1)/3));
end
