package net.chrisdolan.pcgen.drools.pathfinder;
    dialect "java"

import net.chrisdolan.pcgen.drools.input.*;
import net.chrisdolan.pcgen.drools.type.*;

query "Query.Property" (String key)
    PropertyInput(name == key, value : value)
end

rule "PC.UserInput"
    salience 100
    when
        $pc:PCInput()
    then
        for (PropertyInput p : $pc.getProperties())
             insertLogical(p);
        if ($pc.getAlignment() != null)
            insertLogical($pc.getAlignment());
        for (StatInput stat : $pc.getStats())
             insertLogical(stat);
        if ($pc.getRace() != null)
            insertLogical($pc.getRace());
        for (LevelInput lvl : $pc.getLevels())
             insertLogical(lvl);
        if ($pc.getInventory() != null)
            insertLogical($pc.getInventory());

        for (ConditionInput condition : $pc.getConditions())
             insertLogical(condition);
        for (DamageInput damage : $pc.getDamage())
             insertLogical(damage);
        for (ActionInput action : $pc.getActions())
             insertLogical(action);
end

rule "PC.Race.UserInput"
    salience 99
    when
        $r:RaceInput()
    then
        insertLogical(new Race($r.getName()));
        if ($r.getAge() > 0)
            insertLogical(new Age($r.getAge()));
        for (FavoredClassInput fc : $r.getFavoredClasses())
            insertLogical(fc);
        for (TraitInput t : $r.getTraits())
            insertLogical(t);
        for (StatBonusInput sb : $r.getStatBonuses())
            insertLogical(new ChoiceInput("StatBonusChoice", "Race", sb.getName()));
end

rule "PC.Level.UserInput"
    salience 98
    when
        $lvl:LevelInput()
    then
        for (ArchetypeInput archetype : $lvl.getArchetypes())
             insertLogical(archetype);
        if ($lvl.getHitpoints() != null)
            insertLogical($lvl.getHitpoints());
        for (SkillInput skill : $lvl.getSkills())
             insertLogical(skill);
        if ($lvl.getFavoredClassBonus() != null)
             insertLogical($lvl.getFavoredClassBonus());
        for (FeatInput feat : $lvl.getFeats())
             insertLogical(feat);
end

