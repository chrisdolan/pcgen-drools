package net.chrisdolan.pcgen.drools.pathfinder;
    dialect "java"

import net.chrisdolan.pcgen.drools.input.*;
import net.chrisdolan.pcgen.drools.type.*;

query "Query.Ability.All"
    Ability(name : name)
end

declare Ability
    name : String
end
declare AbilityGrant
    ability : Ability
end
declare AbilityReplacement
    ability : Ability
end

rule "Ability.UserInput"
    when
        AbilityInput($n:name)
    then
        insertLogical(new Ability($n));
end

rule "Ability.Feat.UserInput"
    when
        FeatInput($n:name, $t:type)
    then
        insertLogical(new Ability("Feat." + $n + ($t == null ? "" : "." + $t)));
end
rule "Ability.Trait.UserInput"
    when
        TraitInput($n:name)
    then
        insertLogical(new Ability("Trait." + $n));
end

rule "Ability.Grant"
    when
        AbilityGrant($ability : ability)
        not AbilityReplacement(ability.name == $ability.name)
    then
        insertLogical($ability);
end
