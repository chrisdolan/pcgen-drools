package net.chrisdolan.pcgen.drools.pathfinder;
    dialect "java"

import net.chrisdolan.pcgen.drools.input.*;
import net.chrisdolan.pcgen.drools.type.*;

query "Query.Ability.All"
    Ability(name : name)
end

declare Ability
    name : String
end

rule "Ability.UserInput"
    when
        AbilityInput($n:name)
    then
        insertLogical(new Ability($n));
end

rule "Ability.Feat.UserInput"
    when
        FeatInput($n:name, $t:type)
    then
        insertLogical(new Ability("Feat." + $n + ($t == null ? "" : "." + $t)));
end
rule "Ability.Trait.UserInput"
    when
        TraitInput($n:name)
    then
        insertLogical(new Ability("Trait." + $n));
end

rule "Ability.SlowAndSteady"
    when
        Ability(name == "SlowAndSteady")
    then
        insertLogical(new Ability("Speed.IgnoreArmor"));
        insertLogical(new Ability("Speed.IgnoreEncumbrance"));
end

rule "Ability.Toughness"
    when
        Ability(name == "Feat.Toughness")
        PCLevel($lvl:value)
    then
        insertLogical(new Input("Hitpoints", "Toughness", $lvl <= 3 ? 3 : $lvl));
end
