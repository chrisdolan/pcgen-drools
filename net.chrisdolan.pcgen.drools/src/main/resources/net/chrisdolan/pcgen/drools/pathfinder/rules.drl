package net.chrisdolan.pcgen.drools.pathfinder;
    dialect "java"

import net.chrisdolan.pcgen.drools.input.*;
import net.chrisdolan.pcgen.drools.type.*;

rule "Global.Initialization"
    salience 100000 // trigger this rule first of all
    when
    then
        System.out.println("--Session init--");
end

declare Input
   type : String
   subtype : String
   value : int
end
declare ChoiceInput
   type : String
   subtype : String
   value : String
end
declare Stack
   type : String
   subtype : String
   value : int
end
declare StackRule
    type : String
    subtype : String
    rulename : String
end

rule "Stack.Max.Default"
    // this is complicated because without a singleton StackRule to key off, we need to somehow select just one Input.
    // I had to add the hashCode to tie-break between two Inputs with the same value. Before that, I had a bug
    // that ties each added a Stack item 
    when
        $s : Input($t:type, $st:subtype, $v:value, $o:hashCode)
        not Input(type == $t, subtype == $st, value > $v || (value == $v && hashCode < $o))
        not StackRule(type == $t, subtype == $st)
    then
        //System.out.println("max of " + $t + "." + $st + "=" + $v);
        insertLogical(new Stack($t,$st,$v));
end

rule "Stack.Max"
    when
        StackRule($t:type, $st:subtype, rulename == "Max")
        not(not Input(type == $t, subtype == $st))
        Number($max : intValue) from accumulate(Input(type == $t, subtype == $st, $v:value), max($v))
    then
        //System.out.println("max of " + $t + "." + $st + "=" + $v);
        insertLogical(new Stack($t,$st,$max));
end

rule "Stack.Min"
    when
        StackRule($t:type, $st:subtype, rulename == "Min")
        not(not Input(type == $t, subtype == $st))
        Number($min : intValue) from accumulate(Input(type == $t, subtype == $st, $v:value), min($v))
    then
        //System.out.println("min of " + $t + "." + $st + "=" + $v);
        insertLogical(new Stack($t,$st,$min));
end

rule "Stack.Sum"
    when
        StackRule($t:type, $st:subtype, rulename == "Sum")
        not(not Input(type == $t, subtype == $st))
        Number($sum : intValue) from accumulate(Input(type == $t, subtype == $st, $v:value), sum($v))
    then
        //System.out.println("sum of " + $t + "." + $st + "=" + $sum);
        insertLogical(new Stack($t,$st,$sum));
end
